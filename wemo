#!/usr/bin/env perl
use strict;
use warnings;
use WebService::Belkin::WeMo::Discover;
use WebService::Belkin::WeMo::Device;

package Locator;

my $db = "$ENV{HOME}/.wemo.db";

sub find {
    my($class, $name, $refresh) = @_;

    my $client = WebService::Belkin::WeMo::Discover->new;
    my $wemos;
    if (!$refresh && -e $db) {
        $wemos = $client->load($db);
    } else {
        $wemos = $client->search;
        $client->save($db);
    }

    my $matcher = $name ? sub { $_[0] =~ /\Q$name\E/i } : sub { 1 };

    for my $ip (sort keys %$wemos) {
        if ($matcher->($wemos->{$ip}->{name})) {
            return WebService::Belkin::WeMo::Device->new(ip => $ip, db => $db);
        }
    }

    return;
}

package main;
use Getopt::Std;

my($cmd, $name) = @ARGV;

my %commands = (
    'r'      => [ \&cmd_reload, 0 ],
    'reload' => [ \&cmd_reload, 0 ],
    't'      => [ \&cmd_toggle, 1 ],
    'toggle' => [ \&cmd_toggle, 1 ],
    'on'     => [ \&cmd_on, 1 ],
    'off'    => [ \&cmd_off, 1 ],
    'status' => [ \&cmd_status, 1 ],
    's'      => [ \&cmd_status, 1 ],
);

my $command = $commands{$cmd || ''} or die "Usage: wemo [reload|toggle|on|off|status] [name]\n";

my($sub, $want) = @$command;

if ($want) {
    my $wemo = Locator->find($name) or die "Could not locate Wemo\n";
    $sub->($wemo);
} else {
    $sub->();
}

sub cmd_reload {
    Locator->find(undef, 1);
}

sub cmd_toggle { shift->toggleSwitch }
sub cmd_on     { shift->on }
sub cmd_off    { shift->off }

sub cmd_status {
    my $wemo = shift;
    printf "%s at %s is %s\n", $wemo->getFriendlyName, $wemo->{_ip}, ($wemo->isSwitchOn ? "On" : "Off");
}
